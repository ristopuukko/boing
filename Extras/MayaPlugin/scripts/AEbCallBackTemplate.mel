/*
Written by: Risto Puukko <risto.puukko@gmail.com>
*/

//AEbCallBackTemplate.mel

global string $bCallBackTemplateTextField;

global proc AESaveScriptbCallBackScript( string $nodeName ) {
    
    global string $bCallBackTemplateTextField;

    string $script = `text -q -l $bCallBackTemplateTextField`;
    setAttr -type "string" $nodeName $script;
}


global proc AErevertScriptEditorbCallBackScript( string $nodeName ) {
    global string $bCallBackTemplateTextField;
    //string $script = `scrollField -q -tx $bCallBackTemplateTextField`;
    string $script = `text -q -l $bCallBackTemplateTextField`;
    cmdScrollFieldExecuter -e -t $script "boingScriptEditorUI";
}


global proc AESaveEditorScriptbCallBackScript( string $nodeName ) {
    
    global string $bCallBackTemplateTextField;
    string $script = `cmdScrollFieldExecuter -q -t "boingScriptEditorUI"`;

    text -e -l $script $bCallBackTemplateTextField;
    setAttr -type "string" $nodeName $script;
}

global proc AECloseScriptEditorbCallBackScript() {
    
    if (`window -exists "boingEditorUI"`)
    {
        deleteUI boingEditorUI;
    }

}

global proc AEOpenScriptEditorbCallBackScript( string $nodeName ) {
    
    string $script = getAttr($nodeName);

    AECloseScriptEditorbCallBackScript();
    
    window -rtf true -w 400 -h 600 -title "Boing callback script Editor" -menuBar false "boingEditorUI";
    
    string $boingform = `formLayout -numberOfDivisions 100`;
    //string $scriptEd = `cmdScrollFieldExecuter -st "mel" -cco true -sth false -sln true `;//-w 500 -h 400 "boingScriptEditorUI"`;
    cmdScrollFieldExecuter -st "mel" -cco true -sth false -sln true "boingScriptEditorUI" ;
    cmdScrollFieldExecuter -e -t $script "boingScriptEditorUI";

    string $b1 = `button
        -label "Revert"
        -annotation "Restore current saved script."
        -width 98
        -command ("AErevertScriptEditorbCallBackScript(\"" + $nodeName + "\");")`;

    
    string $b2 = `button
        -label "Reset/clear"
        -annotation "Clear the boing callback editor."
        -width 98
        -command ("AEclearScriptEditorbCallBackScript(\"" + $nodeName + "\");")`;

    
    string $b3 = `button
        -label "Save script"
        -annotation "Save the script to the node."
        -width 98
        -command ("AESaveEditorScriptbCallBackScript(\"" + $nodeName + "\");")`;

    
   string $b4 = ` button
        -label "Close"
        -annotation "Closes the window."
        -width 98
        -command ("AECloseScriptEditorbCallBackScript();")`;

    
    string $boingMsgFrame = `frameLayout -labelVisible false`;
    string $boingMsgLine = `messageLine`;
    
    formLayout -edit
    
    -attachForm "boingScriptEditorUI" "top" 1
    -attachForm "boingScriptEditorUI" "left" 1
    -attachForm "boingScriptEditorUI" "bottom" 60
    -attachForm "boingScriptEditorUI" "right" 1

    -attachForm $b1 "bottom" 30
    -attachForm $b1 "left" 2

    -attachForm $b2 "bottom" 30
    -attachForm $b2 "left" 102

    -attachForm $b3 "bottom" 30
    -attachForm $b3 "left" 206

    -attachForm $b4 "bottom" 30
    -attachForm $b4 "left" 310

    -attachNone $boingMsgFrame  "top"
    -attachForm $boingMsgFrame  "left"   0
    -attachForm $boingMsgFrame  "bottom" 0
    -attachForm $boingMsgFrame  "right"  0
    
    $boingform;
    
    showWindow;
}

global proc AEclearScriptEditorbCallBackScript( string $nodeName ) {
    cmdScrollFieldExecuter -e -clr "boingScriptEditorUI";
}

global proc AESaveScriptButtonNew( string $nodeName ) {
    //print("AESaveScriptButtonNew : " + $nodeName + "\n");
    
	rowColumnLayout
        -numberOfColumns 1
        -columnWidth 1 98
        -columnAlign 1 "center"
        -columnAttach 1 "both" 0
        -rowAttach 1 "top" 2;

        button
            -label "Edit script"
            -command ("AEOpenScriptEditorbCallBackScript(\"" + $nodeName + "\");")
            -annotation "Open callback script editor."
            -width 98
            "AEbCallBackOpenScriptEditorUIButton";
    
	setParent ..;

}

global proc AESaveScriptButtonRefresh( string $nodeName ) {

    button
    -e -command ("AEOpenScriptEditorbCallBackScript(\"" + $nodeName + "\");")
    "AEbCallBackOpenScriptEditorUIButton";
    
}


global proc AEbCallBackTemplateNew( string $nodeName ) {


    global string $bCallBackTemplateTextField;
    
    $bCallBackTemplateTextField = `text -w 220 -h 200`;// -l "Callback script"`;
    string $callbackscript = getAttr($nodeName);
    if ($callbackscript == "") $callbackscript = "//Your script here!";

	text -e -bgc .2 .2 .2 -en false -al "left" -l $callbackscript $bCallBackTemplateTextField;
    AESaveScriptbCallBackScript($nodeName);
    setParent ..;

	connectControl $bCallBackTemplateTextField $nodeName;
    
}


global proc AEbCallBackTemplateRefresh(string $nodeName) {

    global string $bCallBackTemplateTextField;
    string $callbackscript = getAttr($nodeName);

	text -e -bgc .2 .2 .2 -en false -al "left" -l $callbackscript $bCallBackTemplateTextField;

    setParent ..;

	connectControl $bCallBackTemplateTextField $nodeName;
}


global proc AEbCallBackTemplate(string $nodeName ) {
    

    editorTemplate -beginScrollLayout;
    
        editorTemplate -collapse false -beginLayout "CallBack basic attributes";
            editorTemplate -addControl Enable;
            editorTemplate -addControl callbacktype;
            editorTemplate -addControl priority;
            editorTemplate -as;
        editorTemplate -endLayout;
        editorTemplate -collapse false -beginLayout "CallBack script";
            //editorTemplate -as;
            editorTemplate -callCustom "AEbCallBackTemplateNew" "AEbCallBackTemplateRefresh" "callbackscript";
            //editorTemplate -as;
            editorTemplate -callCustom "AESaveScriptButtonNew" "AESaveScriptButtonRefresh" "callbackscript";
        editorTemplate -endLayout;

     //editorTemplate -beginLayout (uiRes("m_AElocatorTemplate.kLocatorAttributes")) -collapse 1;
     //AElocatorCommon $nodeName;
     //editorTemplate -endLayout;
     
    {
        editorTemplate -suppress "visibility";
        editorTemplate -suppress "template";
        editorTemplate -suppress "lodVisibility";
        editorTemplate -suppress "ghostingControl";
        editorTemplate -suppress "ghosting";
        editorTemplate -suppress "selectionChildHighlighting";
        editorTemplate -suppress "motionBlur";
        editorTemplate -suppress "visibleInReflections";
        editorTemplate -suppress "visibleInRefractions";
        editorTemplate -suppress "maxVisibilitySamplesOverride";
        editorTemplate -suppress "maxVisibilitySamples";
        editorTemplate -suppress "geometryAntialiasingOverride";
        editorTemplate -suppress "antialiasingLevel";
        editorTemplate -suppress "shadingSamplesOverride";
        editorTemplate -suppress "shadingSamples";
        editorTemplate -suppress "maxShadingSamples";
        editorTemplate -suppress "volumeSamplesOverride";
        editorTemplate -suppress "volumeSamples";
        editorTemplate -suppress "depthJitter";
        editorTemplate -suppress "ignoreSelfShadowing";
        editorTemplate -suppress "primaryVisibility";
        editorTemplate -suppress "compInstObjGroups";
    }

    //AEshapeTemplate $nodeName;
    //AEtransformMain $nodeName;
    //AEtransformNoScroll $nodeName;
    AEdependNodeTemplate $nodeName;
    editorTemplate -addExtraControls;
    editorTemplate -endScrollLayout;
}