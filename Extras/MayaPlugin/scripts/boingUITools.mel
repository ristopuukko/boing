/*
Bullet Continuous Collision Detection and Physics Library Maya Plugin
Copyright (c) 2008 Walt Disney Studios

This software is provided 'as-is', without any express or implied warranty.
In no event will the authors be held liable for any damages arising
from the use of this software.
Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it freely,
subject to the following restrictions:

1. The origin of this software must not be misrepresented; you must
not claim that you wrote the original software. If you use this
software in a product, an acknowledgment in the product documentation
would be appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must
not be misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.

written by Michael Baker <michael@mBakr.com>
*/

//boingUITools.mel - dependent on boingUI.mel

global proc boingUI_addTools()
{
	global string $boingUI_window;
	global string $boingUI_tabs;
	global string $boingUI_helpLine;
	global string $boingUI_toolTabLabel = "Advanced";
	global string $boingUI_toolTab;

	if ( `window -ex $boingUI_window` )
	{
		string $tabs[] = `tabLayout -q -tl $boingUI_tabs`;
		int $toolsTabExists = 0;
		
		//check if Tools tab exists, delete if yes, create if no
		for ($t in $tabs)
		{
			if ($t == $boingUI_toolTabLabel)
			{ 
				deleteUI $boingUI_toolTab;
				//tabLayout -e -sti 1 $boingUI_tabs;
				boingUI_print("Advanced tab removed from main Dynamica window", 1);
				$toolsTabExists = 1;
				break;
			}
		}
		
		if (!$toolsTabExists)
		{
			$boingUI_toolTab = boingUI_createToolsTab();
			tabLayout -e -tl $boingUI_toolTab $boingUI_toolTabLabel $boingUI_tabs;
			//tabLayout -e -st $boingUI_toolTab $boingUI_tabs;
			boingUI_print("Advanced tab added to main Dynamica window", 1);
		}
		
		showWindow $boingUI_window;
	}
	else
	{
		error("Dynamica window not loaded, unable to add Advanced tools");
	}
}

global proc string boingUI_createToolsTab()
{
	global string $boingUI_tabs;
	global float $boingUITools_massMax = 10;
	string $mainForm = `columnLayout -adj 1 -p $boingUI_tabs`;
		text -al "left" -bgc 1.0 0.1 0.1 -l "Advanced Dynamica Tools. Warning: some of these features are experimental!";
		
		boingUI_createFrame("Compute Mass of selected rigid bodies", "Assign computed mass (based on volume) to selected rigid bodies.", 1, 0);
			rowLayout -nc 3;
				text -align "left" -l "Maximum Mass: ";
				floatField -pre 3 -w 50 -v $boingUITools_massMax -ann "Maximum mass value"
					-cc "$boingUITools_massMax = `floatField -q -v massMax`" massMax;
				button -l "Compute Mass" -c "boingUITools_computeMass()";
			setParent ..;
		setParent ..;
	setParent ..;
	
	return $mainForm;
}

global proc boingUITools_computeMass()
{
	global float $boingUITools_massMax;
	string $sel[] = `ls -sl`;
	string $meshes[];
	string $RBs[];
	string $selTmp[] = `ls -sl`;
	float $obVolume;
	float $minVolume = 100000;
	float $maxVolume = 0;
	float $massMin = 1;
	int $i;

	//filter selection
	select -cl;	
	for ($s in $sel) if (mbIsType($s, "dRigidBody")) select -add $s; //if dRigidBody
	$sel = `ls -sl`;
	
	for ($s in $sel)
	{
		$connectedTransforms = mbGetConnectedTransforms($s);
		if (size($connectedTransforms))
		{
			if (mbIsMesh($connectedTransforms[0]))
			{
				$meshes[size($meshes)] = $connectedTransforms[0];
				$RBs[size($RBs)] = $s;
			}
		}
	}
	
	if (!size($meshes)) 
	{
		select -r $selTmp; //restore original selection
		boingUI_print("No connected meshes found in selection.", 2);
		return;
	}
	
	//we are go!
	boingUI_print("Computing mass for " + size($sel) + " rigid bodies...", 0);
	refresh;
	
	for ($i=0;$i<size($meshes);$i++)
	{
		$obVolume = mbMeshVolume($meshes[$i]);
		float $minVolume = 1;
		float $maxVolume = 100;
		float $massMin = 1;
		float $massMax = $boingUITools_massMax;

		if ($obVolume > $maxVolume) $maxVolume = $obVolume;
		//if ($obVolume < $minVolume) $minVolume = $obVolume;
		float $mass = mbScaleFloat($obVolume, $minVolume, $maxVolume, $massMin, $massMax);
		
		if (mbIsKeyed(mbGetShape($RBs[$i])))
		{
			string $curve = mbGetAnimCurve(mbGetShape($RBs[$i]), "mass"); //get mass animation curve
			if ($curve != "")
			{
				float $key[] = mbGetKeyByIndex($curve, 2); //get the 3rd key data
				setKeyframe -at "mass" -t $key[0] -v $mass $RBs[$i]; //assign computed mass to 3rd key
			}
		}
		else
		{
			$m = `getAttr (mbGetShape($RBs[$i]) + ".mass")`;
			if ($m != 0) setAttr ($RBs[$i] + ".mass") $mass;
		}
	}
	
	boingUI_print("Mass computed for " + size($sel) + " rigid bodies...", 0);
}


